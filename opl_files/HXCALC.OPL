HXCALC:
GLOBAL M%
LOCAL A$(255),A,K%
ONERR ER::
ESCAPE OFF
UDG 2,0,0,0,0,0,0,0,31
UDG 0,0,4,14,4,0,14,0,31
CLS
PRINT CHR$(14);CHR$(0);REPT$(CHR$(2),19);
CLOCK(1)
KSTAT 3
A$=""
M%=1
K%=0
L::
DO
 PRINT CHR$(23);CHR$(22);MID$("DecHex",3*M%+1,3);":";
 IF K%<=1 AND A$=""
  CURSOR ON
  K%=GET
  IF K%=2
   M%=1-M%
   K%=0
   GOTO L::
  ELSEIF K%=13
   K%=0
   GOTO L::
  ELSEIF K%=1
   GOTO E::
  ENDIF
 ENDIF
 IF K%>=32 OR K%=8 OR K%=5 OR K%=3
  POKEW $73,$0300
  POKEB $20B0,4
  POKEB $20B1,K%
  POKEW $73,$0002
 ELSE
  POKEW $73,$0200
  POKEB $20B0,4
  POKEW $73,$0001
 ENDIF
 TRAP EDIT A$
 IF ERR=206
E::
  ONERR OFF
  ESCAPE ON
  CLOCK(0)
  KSTAT 1
  RETURN
 ELSEIF ERR
  RAISE ERR
 ENDIF
 K%=0
UNTIL A$<>""
A=HXCLEVL:(0.,%+,A$)
RSLT::
PRINT CHR$(15);CHR$(23);"   =";
IF M%
 PRINT HX$:(A);
ELSE
 PRINT A;
ENDIF
K%=VIEW(3,MID$("DecHex",M%*3+1,3)+":"+A$)
IF K%=1 OR (K%>=%0 AND K%<=%9) OR (((K%>=%A AND K%<=%F) OR (K%>=%a AND K%<=%f)) AND M%=1)
 A$=""
ELSEIF K%>8 AND K%<>13
 IF M%
  A$=HX$:(A)
 ELSE
  A$=GEN$(A,16)
 ENDIF
ELSEIF K%=2
 A$=HXDCCNV$:(A$)
 M%=1-M%
 GOTO RSLT::
ENDIF
GOTO L::

ER::
IF ERR=194
 ONERR OFF
 RAISE ERR
ENDIF
K%=ERR
IF K%=252
 K%=228
ENDIF
PRINT CHR$(15);CHR$(16);ERR$(K%);
K%=0
GOTO L::
