DATE:
LOCAL TD%,TM%,TY%
LOCAL D%,M%,Y%,WK%,I%,K%,ON%
CURSOR OFF :CLS
UDG 0,31,31,17,21,21,21,31,31
UDG 2,0,0,0,0,0,0,0,31
PRINT CHR$(0); REPT$(CHR$(2),14)
CLOCK(1)
RESET::
TD%=DAY :TM%=MONTH :TY%=YEAR
D%=DAY :M%=MONTH :Y%=YEAR
K%=0 :I%=1
TOP::
DO
 ONERR ADJ::
 REM SETUP SCREEN
 AT 1,2 :PRINT DAYNAME$(DOW(D%,M%,Y%))
 AT 5,2 :PRINT RIGHT$(GEN$((D%+100),3),2)
 AT 8,2 :PRINT MONTH$(M%)
 AT 12,2 :PRINT Y%
 AT 17,2 :PRINT "Wk";GEN$(WEEK(D%,M%,Y%),-2)
 AT 8,3 :PRINT RIGHT$(GEN$(Y%,4),2);"."
 AT 11,3 :PRINT RIGHT$(GEN$((DAYS(D%,M%,Y%)-DAYS(1,1,Y%))+1001,4),3)
 AT 1,4 :PRINT "Days To/From:"
 AT 15,4 :PRINT DAYS(D%,M%,Y%)-DAYS(TD%,TM%,TY%);CHR$(26)
GETKEY::
 IF I%>3 :I%=1
 ELSEIF I%<1 :I%=3
 ENDIF :CURSOR ON
 IF I%=1
  AT 5,2 :K%=GET
 ELSEIF I%=2
  AT 8,2 :K%=GET
 ELSE
  AT 12,2 :K%=GET
 ENDIF
 CURSOR OFF
 REM SERVICE KEYPRESS
 REM 5/6 = RIGHT/LEFT ARROW
 IF K%=5 :I%=I%-1 :ON%=0 :GOTO GETKEY::
 ELSEIF K%=6 :I%=I%+1 :ON%=0 :GOTO GETKEY::
 REM 3/4 = UP/DOWN ARROW
 ELSEIF K%=3 :ON%=0
  IF I%=1 :D%=D%+1
   IF D%>31 :D%=1 :M%=M%+1
     IF M%>12 :M%=1 :Y%=Y%+1 :ENDIF
   ENDIF
  ELSEIF I%=2 :M%=M%+1
   IF M%>12 :M%=1 :Y%=Y%+1 :ENDIF
  ELSEIF I%=3 :Y%=Y%+1
   IF Y%>2050 :Y%=1900 :ENDIF
  ENDIF
 ELSEIF K%=4 :ON%=0
  IF I%=1 :D%=D%-1
   IF D%=0 :D%=31 :M%=M%-1
    IF M%=0 :M%=12 :Y%=Y%-1 :ENDIF
    ENDIF
  ELSEIF I%=2 :M%=M%-1
   IF M%=0 :M%=12 :Y%=Y%-1 :ENDIF
  ELSEIF I%=3 :Y%=Y%-1
   IF Y%<1900 :Y%=2050 :ENDIF
  ENDIF
 REM 13 = EXE KEY
 ELSEIF K%=13
  TD%=D% :TM%=M% :TY%=Y%
  I%=1 :K%=0 :ON%=0
 REM 1 = ON/CLEAR
 ELSEIF K%=1 :ON%=ON%+1
  IF ON%=1
   TD%=DAY :TM%=MONTH :TY%=YEAR
   AT 15,4 :PRINT "      "
   AT 15,4 :PRINT DAYS(TD%,TM%,TY%)-DAYS(D%,M%,Y%)
   PAUSE 8
   GOTO RESET::
  ELSE :RETURN
  ENDIF
REM NOT ONE OF THE ABOVE KEYS
 ELSE :CLS :PRINT CHR$(12),CHR$(25)
 AT 1,1 :PRINT "   Valid KEYS are"
 AT 1,2 :PRINT "ON/CLEAR",CHR$(169),CHR$(170),CHR$(127),CHR$(126),"EXE";
 AT 1,4 :PRINT "  Any KEY to RETURN"
 GET :CLS
 UDG 0,31,31,17,21,21,21,31,31
 UDG 2,0,0,0,0,0,0,0,31
 PRINT CHR$(0); REPT$(CHR$(2),14)
 CLOCK(1) :GOTO TOP::
 ENDIF
UNTIL 0
REM ADJUST BAD DATE (i.e. 30 FEB)
ADJ::
 ONERR OFF
 IF K%=3 AND I%=1 :D%=1 :M%=M%+1
  IF M%>12 :M%=1 :Y%=Y%+1 :ENDIF
 ELSEIF K%=3 :D%=DAYS(1,M%+1,Y%)-DAYS(1,M%,Y%)
  IF M%>12 :M%=1 :Y%=Y%+1 :ENDIF
 ELSEIF K%=4 :D%=DAYS(1,M%+1,Y%)-DAYS(1,M%,Y%)
  IF M%=0 :M%=12 :Y%=Y%-1 :ENDIF
 ENDIF
 GOTO TOP::
STOP
