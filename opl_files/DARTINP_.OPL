dartinp%:(i%,j%)
REM Source: DARTINP%.OPL
REM Version 1.3
REM Input routine for score
REM Supports total score and dart by dart input
REM (C) APR 1999 LRS
REM Sluykie@hotmail.com

LOCAL a%,d%,k%,n%,sc%,x%,z%
LOCAL d$(11),l$(3)

REM ***                 Allowed keys                 ***
REM *** ON clears input                              ***
REM *** MODE exist & brings up menu in DARTS         ***
REM *** SPACE separates score per dart in DBD input  ***
REM *** DEL deletes previous character               ***
REM *** D, T doubles and triples                     ***
REM *** 0..9 numeric keys used for score             ***

KSTAT 3
DO
 d$="" : l$="" :d%=0 :n%=0 :sc%=0 :x%=0 :z%=0
 DO
  AT i%,j% :PRINT d$;"_";REPT$(" ",11-d%)
  k%=GET
  k%=-(k%<>41 AND k%<>58)*k%-68*(k%=41)-84*(k%=58)
  IF k%=1
   d$="" :d%=0 :x%=0 :z%=0
  ELSEIF k%=2
   RETURN -1
  ELSEIF (k%=8 AND d%>0)
   d%=d%-1 :d$=LEFT$(d$,d%)
   IF d%<x%
    z%=z%-1 :x%=0 :n%=LOC(d$," ")
    WHILE n%
     x%=x%+n%
     n%=LOC(MID$(d$,n%+1,d%)," ")
    ENDWH
   ENDIF
  ELSEIF (k%=32 AND z%<2 AND x%<d% AND ASC(RIGHT$(d$,1))<58)
   d%=d%+1 :d$=d$+" " :x%=d% :z%=z%+1
  ELSEIF ((k%>47 AND k%<58) OR k%=68 OR k%=84)
   IF z%<2
    IF d%-x%=3 :REM *** auto-insert space ***
     d%=d%+1 :d$=d$+" "
     x%=d% :z%=z%+1
    ENDIF
   ENDIF
   IF d%<11
    IF (x%=d%) OR (x%<d% AND k%<>68 AND k%<>84)
     d%=d%+1 :d$=d$+CHR$(k%)
    ENDIF
   ENDIF
  ENDIF
 UNTIL k%=13 AND d%>0 :REM *** quit input loop if EXE pressed ***

 IF RIGHT$(d$,1)=" "
  d%=d%-1 :d$=LEFT$(d$,d%)
  x%=LOC(d$," ") :z%=z%-1
 ENDIF

 REM *** calculate score from input ***
 a%=0 :k%=1
 IF x% :n%=LOC(d$," ") :ELSE :n%=d%+1 :ENDIF
 DO
  a%=a%+1
  l$=MID$(d$,k%,n%-k%)
  IF LEFT$(l$,1)="D" :REM *** double ***
   k%=2*VAL(MID$(l$,2,LEN(l$)-1))
  ELSEIF LEFT$(l$,1)="T" :REM *** triple ***
   k%=3*VAL(MID$(l$,2,LEN(l$)-1))
  ELSE
   k%=VAL(l$) :REM *** single or total score ***
  ENDIF
  sc%=sc%+k%
  k%=n%+1
  n%=-(a%<z%)*x%-(a%=z%)*(d%+1)
 UNTIL a%>z% :REM *** calc loop ***

UNTIL sc%<181 :REM *** main loop ***

RETURN sc%
