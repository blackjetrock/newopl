INFO:
LOCAL A$(255),P%,M%,RAM%,B%,B1%,B2%
A$="Workspace: "+GEN$(FREE+2370,5)+"  Model: "
B1%=PEEKB($FFE8)
B2%=PEEKB($FFCB)
M%=B1% AND 7
IF M%=7
 M%=7+(B2% AND 7)
ENDIF
P%=B1%/16

IF M%=0
 A$=A$+"CM"
 B%=32
 RAM%=8
ELSEIF M%=1
 A$=A$+"XP"
 B%=32
 RAM%=16
ELSEIF M%=2
 A$=A$+"LA"
 B%=32
 RAM%=32
 IF PEEKB($FFE7)=128
  B%=64
 ENDIF
ELSEIF M%=4
 A$=A$+"350"
 RAM%=96
 B%=32
ELSEIF M%=5
 A$=A$+"LZ64"
 B%=64
 RAM%=64
ELSEIF M%=6
 A$=A$+"LZ"
 B%=64
 RAM%=32
ELSE
 A$=A$+GEN$(M%,3)+CHR$(63)
ENDIF

IF P%
 A$=A$+" POS"
 IF P%=1
  A$=A$+" Normal keyboard"
 ELSEIF P%=4
  A$=A$+" Alpha keyboard"
 ELSE
  A$=A$+" Unknown keyboard type ("+GEN$(P%,5)+")"
 ENDIF
ENDIF


A$=A$+", RAM "+GEN$(RAM%,5)
A$=A$+", ROM "+GEN$(B%,5)
A$=A$+" ver "+HEX$(PEEKB($FFE9))
P%=0
DO
 A$=A$+"     "+CHR$(9)+"Slot "+CHR$(65+P%)+": "
 TRAP OPEN CHR$(P%+65)+":MAIN",A,A$
 IF ERR=0
  B%=$20D7+P%*10
  B1%=PEEKB(B%)
  B2%=PEEKB(B%+1)*8
  IF P%=0
   B2%=RAM%
  ENDIF
  IF P%<>3
   A$=A$+GEN$(SPACE+(2685 AND P%=0),8)+" bytes free  "
   CLOSE
   A$=A$+GEN$(B2%,5)+"Kb "
   IF P%=0
    A$=A$+"Ram"
   ELSE
    IF (B1% AND 66)=2
     A$=A$+"FlashPak"
     IF (PEEKB(B%+8) AND 128)=0
      A$=A$+" NoWrite"
     ENDIF
    ELSE
     IF B1% AND 2
      A$=A$+"DataPak"
     ELSE
      A$=A$+"RamPak"
     ENDIF
     IF (B1% AND 8)=0
      A$=A$+" NoWrite"
     ENDIF
    ENDIF
    IF (B1% AND 32)=0
     A$=A$+" NoCopy"
    ENDIF
    IF (B1% AND 16)=0
     A$=A$+" Boot:"+HEX$(PEEKB(B%+3))+" ver "+HEX$(PEEKB(B%+4))
    ENDIF
   ENDIF
  ELSE
   REM Top slot
   B1%=PEEKB(B%+3)
   B2%=PEEKB(B%+4)
   A$=A$+"Dev="+HEX$(B1%)
   IF B1%=$C0
    A$=A$+" Comms Link"
   ELSEIF B1%=$BF
    A$=A$+" Barcode Reader"
   ELSEIF B1%=$BE
    A$=A$+" Swipe Reader"
   ELSEIF B1%=$BC
    A$=A$+" Comms+Barcode"
   ELSEIF B1%=$B8
    A$=A$+" Comms+Swipe"
   ENDIF
   A$=A$+" ver "+HEX$(PEEKB(B%+4))
  ENDIF
 ELSE
  A$=A$+"Empty"
 ENDIF
 P%=P%+1
UNTIL P%>3
IF PEEKB($2177) AND 1
 A$=A$+", Psion Printer"
ENDIF

IF DISP(1,A$)<>1
 WHILE DISP(0,"")<>1
 ENDWH
ENDIF
