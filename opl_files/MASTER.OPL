MASTER:
LOCAL PEGS,HOLES
LOCAL ANSWER$(10),GUESS$(10)
LOCAL T1$(10),T2$(10)
LOCAL CNT,MARK$(10)
LOCAL X%,P%
LOCAL YN$(1)
SETUP::
PEGS=8 : HOLES=4
START::
CLS
PRINT "* MASTER CHALLENGE *";
HOLES::
AT 1,3
PRINT "HOLES (4-6) :";
INPUT HOLES
IF HOLES<4 OR HOLES>6
 PRINT CHR$(16);
 GOTO HOLES::
ENDIF
PEGS::
AT 1,3
PRINT " PEGS (6-12) : ";
 INPUT PEGS
IF PEGS<6 OR PEGS>12
 PRINT CHR$(16);
 GOTO PEGS::
ENDIF 
AT 1,3 : PRINT REPT$(" ",20);
AT 1,4
PRINT "HOLES=";HOLES;"   PEGS=";PEGS;
X%=GET
CLS
PRINT "   THE CHALLENGE"
PRINT "      BEGINS"+CHR$(33)
AT 1,4
X%=HOLES
ANSWER$=""
DO
 ANSWER$=ANSWER$+CHR$(65+INT(RND*PEGS))
 X%=X%-1
UNTIL(X%=0)
CNT=0
LOOP::
CNT=CNT+1
PRINT NUM$(INTF(CNT),-3);": ";
INPUT GUESS$
AT 14,4
IF GUESS$=""
 CLS
 PRINT "GIVE UP (Y/N)";CHR$(63);" ";
 YN$=GET$
 IF YN$="Y" : GOTO GIVEUP:: : ENDIF
 CLS
 AT 1,4
 CNT=CNT-1
 GOTO LOOP::
ENDIF
IF GUESS$=ANSWER$ : GOTO OK:: : ENDIF
IF LEN(GUESS$)<>HOLES
 PRINT CHR$(16);
 CNT=CNT-1
 AT 1,4
 GOTO LOOP::
ENDIF
MARK$=""
T1$="" : T2$=""
REM SCORE BLACKS
X%=1
DO
IF MID$(ANSWER$,X%,1)=MID$(GUESS$,X%,1)
 T1$=T1$+"."
 T2$=T2$+"*"
 MARK$=MARK$+"X"
ELSE
 T1$=T1$+MID$(ANSWER$,X%,1)
 T2$=T2$+MID$(GUESS$,X%,1)
ENDIF
X%=X%+1
UNTIL(X%>HOLES)
REM SCORE WHITES
X%=1
DO
 P%=LOC(T1$,MID$(T2$,X%,1))
 IF P%<>0
  T1$=MID$(T1$,1,P%-1)+"."+MID$(T1$,P%+1,99)
  MARK$=MARK$+"O"
ENDIF
X%=X%+1
UNTIL(X%>HOLES)
PRINT MARK$
GOTO LOOP::
OK::
CLS
PRINT "********************";
PRINT "  CONGRATULATIONS";CHR$(33)
PRINT "********************";
END::
PRINT "ANSWER = ";ANSWER$;
AT 18,4 : PRINT CNT;
X%=GET
RETURN
GIVEUP::
CLS
PRINT "********************";
PRINT "   YOU GAVE UP";CHR$(33)
PRINT "********************";
GOTO END::
