border:
LOCAL ch$(232),w$(20),bl$(20),p%,a%,k%,b%
a%=1 :p%=0 :b%=10
bl$=REPT$(CHR$(32),20)

REM                  Change the screen border for
REM                  XP procedures on the LZ.
REM                  Glyn Pollington. 1990.

CLS
AT 1,1 :PRINT "       Border"
AT 1,2 :PRINT "    G.Pollington."
AT 1,3 :PRINT "Use cursor keys then"
AT 1,4 :PRINT "   EXE to select."

DO
   ch$=ch$+CHR$(p%)
   p%=p%+1
   IF p%=8 :p%=32 :ENDIF
UNTIL p%=256

CLS

DO
   AT 3,4 :PRINT "Select Character"
   AT 9,3 :PRINT CHR$(127),CHR$(126)

   IF b%<1 :b%=1 :a%=a%-1
      ELSEIF b%>20 :b%=20 :a%=a%+1
   ENDIF

   AT 1,2  :PRINT bl$
   AT b%,2 :PRINT CHR$(169)

   IF a%<1 :a%=1
   ELSEIF a%>213 :a%=213
   ENDIF

   w$=MID$(ch$,a%,20)
   AT 1,1 :PRINT w$

   k%=KEY
   IF k%=6 :b%=b%+1
      ELSEIF k%=5 :b%=b%-1
   ENDIF

UNTIL k%=13

p%=ASC(MID$(ch$,(a%+b%-1),1))
POKEB $2099,p%
bl$=REPT$(CHR$(p%),20)
CLS
AT 1,1 :PRINT bl$
AT 1,2 :PRINT CHR$(p%);CHR$(p%);"   New Border   ";CHR$(p%);CHR$(p%)
AT 1,3 :PRINT CHR$(p%);CHR$(p%);"    Selected    ";CHR$(p%);CHR$(p%)
AT 1,4 :PRINT bl$
GET

