MSTRHUM:
LOCAL A$(8),M$(20,9),B$(6),S%(3)
LOCAL C%,D%,M%,K%
LOCAL A%

A%=GS%
DO
 M$(A%)=REPT$(".",HL%)+"  "
 A%=A%-1
UNTIL A%=0

S%(1)=ADDR(A$)
S%(2)=HL%*256+CL%
S%(3)=ADDR(B$)+1

M%=1
D%=1
B$=""
DO
 B$=B$+CHR$(RND*(CL%+1))
UNTIL LEN(B$)=HL%
CLS

DO
C%=1
A$=REPT$(".",HL%)
AT 12,1
IF M%<10
 PRINT " ";
ENDIF
PRINT M%;"/";GS%

PR::
CURSOR OFF
AT 1,1
PRINT M$(D%);CHR$(32-(32 AND D%>1))
PRINT M$(D%+1);CHR$(32-(31 AND D%+2<M%));

DO
P::
CURSOR OFF
AT 14-HL%/2,2
PRINT A$;
AT 13+C%-HL%/2,2
KSTAT 3
CURSOR ON
K%=GET
IF K%=5
 C%=C%-1+(HL% AND C%=1)
ELSEIF K%=6
 C%=C%+1-(HL% AND C%=HL%)
ELSEIF K%=3
 IF D%>1
  D%=D%-1
  GOTO PR::
 ENDIF
ELSEIF K%=4
 IF D%+2<M%
  D%=D%+1
  GOTO PR::
 ENDIF
ELSEIF (K%>=%0 AND K%<=%0+CL% OR K%=8) AND C%>0
 IF K%=8
  K%=%.
 ENDIF
 A$=LEFT$(A$,C%-1)+CHR$(K%)+RIGHT$(A$,HL%-C%)
 C%=C%+1-(HL% AND C%=HL%)
ELSEIF K%=1
 IF A$=REPT$(".",HL%)
  K%=MSTRQT%:
  IF K%=%Y
   RETURN 1
  ENDIF
  GOTO P::
 ENDIF
 A$=REPT$(".",HL%)
 C%=1
ENDIF
UNTIL K%=13 AND A$<>REPT$(".",HL%)
CURSOR OFF
M$(M%)=A$
A$=""
A%=1
DO
 K%=ASC(MID$(M$(M%),A%,1))
 IF K%=%.
  A$=A$+CHR$(CL%+1)
 ELSE A$=A$+CHR$(K%-%0)
 ENDIF
A%=A%+1
UNTIL A%>HL%
MSTREVL:(ADDR(S%()))
M$(M%)=M$(M%)+" "+CHR$(ASC(MID$(A$,HL%+1,1))+48)+CHR$(48+ASC(RIGHT$(A$,1)))
M%=M%+1
IF M%>2
 D%=M%-2
ELSE D%=1
ENDIF
UNTIL M%>GS% OR LEFT$(A$,HL%)=B$

NM%=NM%+1
SM%=SM%+M%-1
AT 11,1
IF LEFT$(A$,HL%)=B$
 PRINT "Fnd:";M%-1;
ELSE
 PRINT "It was";
 SM%=SM%+1
 AT 14-HL%/2,2
 A%=1
 DO
  PRINT ASC(MID$(B$,A%,1));
  A%=A%+1
 UNTIL A%>HL%
ENDIF

DO
 AT 1,1
 PRINT M$(D%);CHR$(32-(32 AND D%>1))
 PRINT M$(D%+1);CHR$(32-(31 AND D%+2<M%));
 K%=GET
 D%=D%-(K%=4 AND D%+2<M%)+(K%=3 AND D%>1)
UNTIL K%<3 OR K%>4
