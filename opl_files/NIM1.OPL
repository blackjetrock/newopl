NIM1:
REM LZ procedure
LOCAL BRD%,M%,K%,HC%,G%

UDG 0,16,16,16,16,16,16,16,16
UDG 1,20,20,20,20,20,20,20,20
UDG 2,21,21,21,21,21,21,21,21
loop::
CLS
BRD%=RND*40+11
G%=(BRD%-1)/3
PRINT GEN$(BRD%,-2),REPT$(CHR$(2),G%);CHR$(BRD%-G%*3-1);
AT 1,3
PRINT "Do you want to go   first ";CHR$(63);"   (Y/N)";
keylp::
K%=ASC(UPPER$(GET$))
IF K%=1
 RETURN
ENDIF
IF K%=2
 GOTO loop::
ENDIF
IF K%=%N
 HC%=1
ELSEiF K%=%Y
 HC%=-1
ELSE
 GOTO keylp::
ENDIF
DO
 PRINT CHR$(23);CHR$(22);"  ";
 IF HC%=1
  G%=(BRD%-1)/4
  IF BRD%=G%*4+1
   DO
    M%=3*RND+1
   UNTIL M%<=BRD%
  ELSE
   M%=BRD%-G%*4-1
  ENDIF
  PRINT "I";
 ELSE
  PRINT "Press 1,2 or 3"
  DO
   K%=ASC(UPPER$(GET$))
  UNTIL K%>=%U AND K%<=%W AND K%-%T<=BRD%
  M%=K%-%T
  PRINT CHR$(22);"  You";
 ENDIF
 PRINT " take",M%,"stick";
 IF M%>1
  PRINT "s";
 ENDIF
 DO
  BRD%=BRD%-1
  AT 1,1
  IF BRD%=0
   PRINT " 0  ";
  ELSE
   G%=(BRD%-1)/3
   PRINT GEN$(BRD%,-2),REPT$(CHR$(2),G%);CHR$(BRD%-G%*3-1),
  ENDIF
  PAUSE -5
  M%=M%-1
 UNTIL M%=0
 HC%=-HC%
UNTIL BRD%=0
AT 1,2
IF HC%=1
 PRINT "I";
ELSE
 PRINT "You";
ENDIF
PRINT " have won"
PRINT "Another game ";CHR$(63);" (Y/N)"
DO
 K%=ASC(UPPER$(GET$))
 IF K%=%Y
  GOTO loop::
 ENDIF
UNTIL K%=%N OR K%=1
