dartslz:
REM Source: DARTSLZ.OPL
REM Version 1.3
REM Darts computer for the Psion Organiser 2 LZ/LZ64
REM (C) APR 1999 LRS
REM Sluykie@hotmail.com

LOCAL p$(2,8),u$(20),g$(8),outshot$(9)
LOCAL file%,n%,k%,p%,pack%,r%,s%,t%,w%
LOCAL g%(2),s%(4),out%(2),u%(8),d%(8)

CLS :AT 7,2 :PRINT "DARTS LZ"
AT 3,4 :PRINT "¸ LR Sluyk 04.99"

ESCAPE OFF

REM *** define & open outshot file ***
pack%=%D :file%=0 :outshot$=":DARTS"
DO
 pack%=pack%-1
 IF EXIST(CHR$(pack%)+outshot$)
  file%=1
  OPEN CHR$(pack%)+outshot$,A,g$,x$,y$,z$
 ENDIF
UNTIL file% OR pack%=%A

REM *** define UDG's ***
UDG 0,3,3,3,3,3,3,3,3
UDG 1,31,27,27,27,27,27,27,27
UDG 2,31,24,24,24,24,24,24,31
UDG 3,3,3,3,3,3,3,3,31
UDG 4,31,27,27,27,27,27,27,31
UDG 5,27,27,27,27,27,27,27,31
UDG 6,31,3,3,3,3,3,3,31
UDG 7,31,3,3,3,3,3,3,3
u$="15007263502325704543"

REM *** setup the game ***
nwgme::
CLS
PRINT "Player 1:";:INPUT p$(1)
PRINT "Player 2:";:INPUT p$(2)
PRINT "Start with"
AT 1,4 :k%=MENUN(1,"301,501,1001")
IF k%=0 :RETURN :ENDIF
g%(1)=-(k%=1)*301-(k%=2)*501-(k%=3)*1001 :g%(2)=g%(1)
s%(1)=0 :s%(2)=0 :s%(3)=0 :s%(4)=0
out%(1)=0 :out%(2)=0
w%=0 :p%=-1 :n%=1 :s%=0

DO
 IF (s%<>-1 OR k%=3) :REM *** skip this if MODE pressed and not TBM ***
  g$=NUM$(g%(1),-4)+NUM$(g%(2),-4) :r%=9
  DO
   r%=r%-1
   u%(r%)=32
   d%(r%)=32
   t%=ASC(MID$(g$,r%,1))
   IF t%<>32
    u%(r%)=VAL(MID$(u$,2*(t%-48)+1,1)) :REM *** upper part of large number ***
    d%(r%)=VAL(MID$(u$,2*(t%-48)+2,1)) :REM *** lower part of large number ***
   ENDIF
  UNTIL r%=1
 ENDIF

 CLS
 AT (9-LEN(p$(1)))/2+2,1 :PRINT p$(1)
 AT 11,1 :PRINT CHR$(62+2*p%) 
 AT (11-LEN(p$(2)))/2+12,1 :PRINT p$(2) 
 r%=1 :DO :r%=r%+1 :AT 11,r% :PRINT "|" :UNTIL r%=3
 
 REM *** print goal, last score and score before that for both players ***
 REM *** player 1 ***
 AT 1,2 :PRINT CHR$(u%(1));CHR$(u%(2));CHR$(u%(3));CHR$(u%(4))
 AT 1,3 :PRINT CHR$(d%(1));CHR$(d%(2));CHR$(d%(3));CHR$(d%(4))
 AT 5,2 :PRINT MID$("* ",2+out%(1),1)
 AT 7,2 :PRINT NUM$(s%(1),-3)
 AT 7,3 :PRINT NUM$(s%(2),-3)
 REM *** player 2 ***
 AT 12,2 :PRINT CHR$(u%(5));CHR$(u%(6));CHR$(u%(7));CHR$(u%(8))
 AT 12,3 :PRINT CHR$(d%(5));CHR$(d%(6));CHR$(d%(7));CHR$(d%(8))
 AT 16,2 :PRINT MID$("* ",2+out%(2),1)
 AT 18,2 :PRINT NUM$(s%(3),-3)
 AT 18,3 :PRINT NUM$(s%(4),-3)

 AT 1,4 :PRINT n%;">"
 t%=LEN(NUM$(n%,12))+2 :REM *** screen position for inputcursor ***
 s%=DARTINP%:(t%,4)

 IF s%<>-1 :REM *** score entered ***
  s%(4+2*p%)=s%(3+2*p%)
  s%(3+2*p%)=s%
  t%=g%(2+p%)-s%(3+2*p%)
  g%(2+p%)=-(t%=0 OR t%>1)*t%-(t%<0 OR t%=1)*g%(2+p%)
  IF g%(2+p%)=0 :w%=2+p% :ENDIF
  REM *** check for finish ***
  t%=g%(2+p%)
  out%(2+p%)=NOT(t%>170 OR t%=169 OR t%=168 OR t%=166 OR t%=165 OR t%=163 OR t%=162 OR t%=159)
  p%=NOT p%
  n%=n%-(p%=-1)

 ELSE :REM *** MODE pressed, s%=-1 ***
  CLS :AT 6,1 :PRINT "Darts menu"
  k%=MENUN(2,"Outshot advice,Look up for,(Take back move),New game  ,Quit darts")
  IF (k%=1) OR (k%=2)
   IF file%
    IF k%=2
     PRINT "Look up finish for  >"
     s%=DARTINP%:(2,2)
    ELSE :s%=g%(2+p%)
    ENDIF
    FIRST :r%=FIND("G="+NUM$(s%,-3))
    IF (k%=1) AND NOT(out%(2+p%)) :REM *** no finish possible? ***
     AT 6,2 :PRINT "No finish"
     AT 5,3 :PRINT "possible yet"
    ELSE
     PRINT "Outshot advice" :PRINT s%;":",
     IF r% :PRINT A.x$,A.y$,A.z$
     ELSE :PRINT "unknown"
     ENDIF
    ENDIF
   ELSE :AT 5,2 :PRINT "Outshot file" :AT 6,3 :PRINT "not found"
   ENDIF
   GET
  ELSEIF (k%=3 AND n%+p%>0 AND s%(3+2*(NOT p%))>-1)
   p%=NOT p%
   n%=n%+(p%=0) :REM *** decrease turn number if P.2 re-enters ***
   g%(2+p%)=g%(2+p%)+s%(3+2*p%)
   s%(3+2*p%)=s%(4+2*p%)
   s%(4+2*p%)=-999 :REM *** control number ***
   t%=g%(2+p%) :REM *** recalc out%() ***
   out%(2+p%)=NOT(t%>170 OR t%=169 OR t%=168 OR t%=166 OR t%=165 OR t%=163 OR t%=162 OR t%=159)
  ELSEIF k%=4
   AT 4,2 :PRINT "New game? Y/N"
   k%=GET
   IF k%=%y OR k%=%Y :GOTO nwgme:: :ENDIF
  ELSEIF k%=5
   AT 9,2 :PRINT "QUIT"
   AT 1,4 :PRINT "Are you sure? Y/N"
   k%=GET
   IF k%=%y OR k%=%Y :STOP :ENDIF
  ENDIF
 ENDIF

UNTIL w%

CLS
AT 2,2 :PRINT "We have a winner!!"
AT (20-LEN(p$(w%)))/2+1,3 :PRINT p$(w%)
GET
