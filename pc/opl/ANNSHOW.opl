annshow:
REM Source: ANNSHOW.OPL
REM Version 1.3
REM Shows all entries in dbase$ in a neat way
REM (C) OCT 1998 LRS
REM L.R.Sluijk@student.utwente.nl
REM Revised APR 1999

REM& EXTERNAL dbase$
LOCAL rec%,h%,k%,t%,d$(2,4)

REM calculate first upcoming event near current date
REM using binary search. Dbase$ has to be sorted first
REM for binary search to work properly, otherwise the
REM result makes no sense.
REM After the loop, rec% holds the first upcoming event
REM or if all events are expired, the last event in the list

d$(2)=RIGHT$("0"+NUM$(MONTH,2),2)+RIGHT$("0"+NUM$(DAY,2),2) :REM current date in MMDD
h%=1 :REM head of list
t%=COUNT :REM tail of list
rec%=h%+(t%-h%)/2 :REM position halfway the list
WHILE ((t%-h%)>0) AND (COUNT) :REM at least 2 records in list
 POSITION rec%
 d$(1)=MID$(A.date$,5,4) :REM date from dbase$ in MMDD
 IF d$(1)=d$(2)
  BREAK :REM break out of loop without recalculating rec%
 ELSEIF d$(1)<d$(2) :REM discard head of list
  h%=rec%+1
 ELSEIF d$(1)>d$(2) :REM discard tail of list
  t%=rec%
 ENDIF
 rec%=h%+(t%-h%)/2 :REM recalculate element in center of list
ENDWH

k%=-1
WHILE (k%<>1) AND (COUNT)
 POSITION rec% :h%=2
 DO
  t%=MIN(13,LOC(A.event$,"(")-2)
  IF t%<1 :t%=13 :ENDIF
  AT 1,h% :PRINT CHR$(26);LEFT$(A.event$,t%)
  IF NOT EOF
   REM *** print day right justified without preceding zeroes ***
   AT 15,h% :PRINT NUM$(VAL(MID$(A.date$,7,2)),-2)
   t%=VAL(MID$(A.date$,5,2))
   AT 18,h% :REM *** Dbase date < current date then lower case, else upper case ***
   IF MID$(A.date$,5,4)<RIGHT$("0"+NUM$(MONTH,2),2)+RIGHT$("0"+NUM$(DAY,2),2)
    PRINT LOWER$(MONTH$(t%))
   ELSE :PRINT UPPER$(MONTH$(t%))
   ENDIF
  ENDIF
  NEXT :h%=h%+1
 UNTIL h%>4
 k%=GET
 IF k%=2
  IF rec%=1 :rec%=COUNT-2 :ELSE :rec%=1 :ENDIF :REM Show first/last 3 entries when MODE is pressed
 ENDIF
 IF k%>2 AND k%<7 :rec%=rec%+(k%=3)-(k%=4)+3*(k%=5)-3*(k%=6)
  IF rec%<1 :rec%=1 :ELSEIF rec%>COUNT :rec%=rec%+(k%=4)+3*(k%=6) :ENDIF
 ENDIF
ENDWH
