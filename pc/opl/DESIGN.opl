DESIGN:
REM (c) David J. Woolnough
REM Ver 1.1 - AUGUST 1990
REM Graphics 0-3 are used by the program
REM To allow 0-3 to be used change line 41
REM G%<51 to g%<48
GLOBAL X%,A%(8),B%(40),N%,P%,N$(10)
LOCAL C%(8),K%,L%,M%,G%,PS%,ST%
LOCAL SPA$(16)
SPA$=REPT$(" ",16)
X%=2 :PS%=1
OPEN"A:UDG",A,UNAM$,A1%,A2%,A3%,A4%,A5%,A6%,A7%,A8%
CLS :PRINT"Loading Graphics" :AT 3,2 :PRINT"Please Wait"
N$="BAR" :P%=1 :GUPDATE:
N$="W/SOFT" :P%=2 :GUPDATE:
CLOSE
CLS :PRINT CHR$(2);" UDG Designer ";CHR$(2) :AT 1,2 :PRINT" Ver 1.1 08/90" :GET
BEGIN::
CLS :PRINT"UDGS:";
L%=1
DO
PRINT CHR$(L%-1);
L%=L%+1
UNTIL L%=9
AT 1,2 :PRINT SPA$ :AT 1,2 :PRINT"Update (Y/N)";CHR$(63)
KSTAT 1
TRYB::
G%=GET
IF G%=%N OR G%=%Q
CLS :PRINT"Bye ..." :PAUSE -50 :KEY
RETURN
ELSEIF G%=%Y OR G%=13
GOTO TRYA::
ELSE GOTO TRYB::
ENDIF
TRYA::
AT 1,2 :PRINT SPA$ :AT 1,2 :PRINT"Which one:(3/7)";CHR$(63)
KSTAT 3
G%=GET
KSTAT 1
IF G%<51 OR G%>55
AT 1,2 :PRINT CHR$(15);"Out of Range";CHR$(33) :PAUSE 30 :GOTO TRYA::
ENDIF
N%=G%-48
CLS :PRINT CHR$(126);CHR$(N%);CHR$(127)
AT 5,1 :PRINT"Change (Y/N)"
G%=GET
IF G%=%N
GOTO BEGIN::
ENDIF
START::
CLS :PRINT CHR$(126);CHR$(N%);CHR$(127);"  UDG";CHR$(1);"Update"
AT 1,2 :PRINT"H";CHR$(1);"Help"
UPDATE:
INP::
G%=KEY
IF G%>0
GOTO EXIT::
ENDIF
ST%=B%(PS%)
B%(PS%)=1
L%=1
DO
UPDATE:
G%=KEY
IF G%>0
B%(PS%)=ST%
GOTO EXIT::
ENDIF
L%=L%-1
UNTIL L%=0
B%(PS%)=0
UPDATE:
G%=KEY
IF G%>0
B%(PS%)=ST%
GOTO EXIT::
ENDIF
B%(PS%)=ST%
GOTO INP::
EXIT::
UPDATE:
IF G%=81 :REM RET
AT 1,2 :PRINT SPA$ :AT 1,2 :PRINT"Quit (Y/N)";CHR$(63)
G%=GET
IF G%=%N
AT 1,2 :PRINT SPA$
GOTO START::
ELSE GOTO BEGIN::
ENDIF
ELSEIF G%=3 :REM UP
IF PS% >35
PS%=PS%-35
BEEP 50,100
GOTO INP::
ENDIF
PS%=PS%+5
ELSEIF G%=4 :REM DOWN
IF PS% <6
PS%=PS%+35
BEEP 50,100
GOTO INP::
ENDIF
PS%=PS%-5
ELSEIF G%=6 :REM RIGHT
IF PS%=1
PS%=40
GOTO INP::
ENDIF
PS%=PS%-1
ELSEIF G%=5 :REM LEFT
IF PS%=40
PS%=1
GOTO INP::
ENDIF
PS%=PS%+1
ELSEIF G%=73 :REM INVERT
AT 1,2 :PRINT SPA$ :AT 1,2 :PRINT"-Invert-" :PAUSE 10
L%=40
DO
IF B%(L%)=1
B%(L%)=0
ELSE B%(L%)=1 
ENDIF
L%=L%-1
UNTIL L%=0
GOTO START::
ELSEIF G%=%C :REM CLEAR
AT 1,2 :PRINT SPA$ :AT 1,2 :PRINT"Clear (Y/N)";CHR$(63)
G%=GET
IF G%=%N
GOTO START::
ELSE AT 1,2 :PRINT SPA$ :AT 1,2 :PRINT"-Clear-" :PAUSE 10
ENDIF
L%=40
DO
B%(L%)=0
L%=L%-1
UNTIL L%=0
GOTO START::
ELSEIF G%=%M :REM MIR
AT 1,2 :PRINT SPA$ :AT 1,2 :PRINT"-Mirror-" :PAUSE 10
L%=41
DO
M%=1
DO
C%(M%)=B%(L%-M%)
M%=M%+1
UNTIL M%>5
M%=1
DO
B%(L%-M%)=C%(6-M%)
M%=M%+1
UNTIL M%>5
L%=L%-5
UNTIL L%<6
GOTO START::
ELSEIF G%=%F :REM FLIP
AT 1,2 :PRINT SPA$ :AT 1,2 :PRINT"-Flip-" :PAUSE 10
L%=5
DO
M%=41 :K%=1
DO
C%(K%)=B%(M%-L%)
M%=M%-5 :K%=K%+1
UNTIL K%>8
M%=41 :K%=8
DO
B%(M%-L%)=C%(K%)
M%=M%-5 :K%=K%-1
UNTIL K%<1
L%=L%-1
UNTIL L%<1
GOTO START::
ELSEIF G%=%D
UDGSAVE:
GOTO START::
ELSEIF G%=%N :REM NUMBERS
CLS :L%=1 :M%=1
DO
IF M%<12
AT M%,1
ELSE AT M%-12,2
ENDIF
PRINT A%(L%)
L%=L%+1 :M%=M%+3
UNTIL L%>8
KEY:
GOTO START::
ELSEIF G%=%H
DESINF:
GOTO START::
ELSEIF G%=2
IF B%(PS%)=0
B%(PS%)=1
ELSE B%(PS%)=0
ENDIF :ENDIF
GOTO INP::
